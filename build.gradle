// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.architecture_components_version = '2.1.0'
    ext.support_libraries_version = '1.0.0'
    ext.espresso_version = '3.1.0-alpha4'
    ext.kotlin_version = '1.4.30'
    ext.coroutines_version = '1.4.2'
    // Pinning the last working version of the service-telemetry component until we decide
    // what we want to do with telemetry in the app.
    ext.mozilla_components_version_telemetry = '57.0.9'

    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    // The version number is duplicated in the detekt block. We are unable to use constants due to
    // limitations of the gradle plugin. We've decided to stick with duplication for now since the
    // other methods involve using undocumented APIs
    // https://docs.gradle.org/current/userguide/plugins.html#plugins_dsl_limitations
    id("io.gitlab.arturbosch.detekt").version("1.16.0")
}

detekt {
    // The version number is duplicated, please refer to plugins block for more details
    version = "1.16.0"
    buildUponDefaultConfig = true
    input = files("$projectDir/app")
    config = files("$projectDir/quality/detekt.yml")
    baseline = file("$projectDir/quality/detekt-baseline.xml")

    reports {
        html {
            enabled = true
            destination = file("$projectDir/build/reports/detekt.html")
        }
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
        maven {
            url "https://nightly.maven.mozilla.org/maven2"
        }
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        jcenter() {
            content {
                ////////////////////////////////////////////////////////////////////////////////
                // JCenter is going away. Please do not add any new dependencies here.
                // https://jfrog.com/blog/into-the-sunset-bintray-jcenter-gocenter-and-chartcenter/
                ////////////////////////////////////////////////////////////////////////////////

                // Used by detekt
                // Issue for publishing to maven central: https://github.com/Kotlin/kotlinx.html/issues/173
                // Related detekt issue: https://github.com/detekt/detekt/issues/3461
                includeVersion("org.jetbrains.kotlinx", "kotlinx-html-jvm", "0.7.2")
                includeVersion("org.jetbrains.kotlinx", "kotlinx-html-common", "0.7.2")

                // Fastlane
                // Doesn't seem to be available on Maven Central yet, and I couldn't find an issue for it
                // https://github.com/fastlane/fastlane
                includeVersion("tools.fastlane", "screengrab", "2.0.0")
                // https://github.com/jraska/Falcon/issues/52
                includeVersion("com.jraska", "falcon", "2.1.1")
            }
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.31.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}
