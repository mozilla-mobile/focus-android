// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.architecture_components_version = '1.1.1'
    ext.support_libraries_version = '28.0.0'
    ext.espresso_version = '3.0.1'
    ext.kotlin_version = '1.2.71'
    ext.geckoview_revision = '33bec02bb81f303b8e3d22586b9e67690e939dbb'
    ext.geckoview_version = '62.0.3.20181001191808' // GeckoView 62 RelBranch from 2018.10.01
    ext.spotbugs_version = '3.1.2'
    ext.mozilla_components_version = '0.26.0'

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.1"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    // The version number is duplicated in the detekt block. We are unable to use constants due to
    // limitations of the gradle plugin. We've decided to stick with duplication for now since the
    // other methods involve using undocumented APIs
    // https://docs.gradle.org/current/userguide/plugins.html#plugins_dsl_limitations
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC8"
    id "org.jetbrains.kotlin.kapt" version "1.2.50"
}

detekt {
    // The version number is duplicated, please refer to plugins block for more details
    version = "1.0.0.RC6-3"
    profile("main") {
        input = "$projectDir"
        config = "$projectDir/quality/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
        output = "$projectDir/app/build/reports/detekt"
    }
}

allprojects {
    repositories {
        google()
        maven {
            url "https://maven.mozilla.org/maven2"
        }
        jcenter()
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.21.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}
