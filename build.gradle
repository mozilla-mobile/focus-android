// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.support_libraries_version = '26.1.0'
    ext.architecture_components_version = '1.0.0'
    ext.espresso_version = '3.0.1'
    ext.kotlin_version = '1.2.0'

    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.3.3'
        classpath 'com.dicedmelon.gradle:jacoco-android:0.1.2'

        classpath 'org.ajoberstar:grgit:1.5.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

plugins {
    // The version number is duplicated in the detekt block. We are unable to use constants due to
    // limitations of the gradle plugin. We've decided to stick with duplication for now since the
    // other methods involve using undocumented APIs
    // https://docs.gradle.org/current/userguide/plugins.html#plugins_dsl_limitations
    id "io.gitlab.arturbosch.detekt" version "1.0.0.RC5-6"
    id "org.jetbrains.kotlin.kapt" version "1.2.0"
}

detekt {
    // The version number is duplicated, please refer to plugins block for more details
    version = "1.0.0.RC5-6"
    profile("main") {
        input = "$projectDir"
        config = "$projectDir/quality/detekt.yml"
        filters = ".*test.*,.*/resources/.*,.*/tmp/.*"
    }
}

allprojects {
    repositories {
        jcenter()
        maven { url 'https://maven.google.com' }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}


configurations {
    ktlint
}

dependencies {
    ktlint "com.github.shyiko:ktlint:0.14.0"
}

task ktlint(type: JavaExec, group: "verification") {
    description = "Check Kotlin code style."
    classpath = configurations.ktlint
    main = "com.github.shyiko.ktlint.Main"
    args "**/*.kt"
}
